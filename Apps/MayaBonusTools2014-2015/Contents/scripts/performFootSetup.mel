// Copyright (C) 1997-2013 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//  Creation Date:  Apr, 2003
//
//  Procedure Name:
//      performFootSetup
//
//  Description:
//        
//         
//  Input Arguments:
//        $option : Whether to set the options to default values.
//  Return Value:
//        None
//

global proc getFootSetupControl(string $control){
	string $selection[] = `ls -sl`;
	
	if ($control == "knee"){
		textFieldButtonGrp -edit 
			-text ($selection[0]) 
			kneeControlBtn;
	}else{
		textFieldButtonGrp -edit 
			-text ($selection[0]) 
			footControlBtn;
	}

}



proc setOptionVars (int	$forceFactorySettings)
{

	if ($forceFactorySettings || !`optionVar -exists "footSetupHeel"`)
		optionVar -intValue "footSetupHeel" 1;

	if ($forceFactorySettings || !`optionVar -exists "footSetupStyle"`)
		optionVar -intValue "footSetupStyle" 1;

	if ($forceFactorySettings || !`optionVar -exists "footControl"`)
		optionVar -stringValue "footControl" "";

	if ($forceFactorySettings || !`optionVar -exists "kneeControl"`)
		optionVar -stringValue "kneeControl" "";

}

global proc footSetupSetup (string $parent, int $forceFactorySettings)
{
	setOptionVars($forceFactorySettings);
	setParent $parent;

	radioButtonGrp
        -edit
        -select `optionVar -query footSetupHeel`
        footSetupHeel;

	radioButtonGrp
        -edit
        -select `optionVar -query footSetupStyle`
        footSetupStyle;

	textFieldButtonGrp -edit
		-text `optionVar -query footControl`
		footControlBtn;

	textFieldButtonGrp -edit
		-text `optionVar -query kneeControl`
		kneeControlBtn;

}

global proc footSetupCallback (string $parent, int $doIt)
{	
	setParent $parent;

	optionVar -intValue "footSetupHeel"
		`radioButtonGrp -query -select footSetupHeel`;

	optionVar -intValue "footSetupStyle"
		`radioButtonGrp -query -select footSetupStyle`;

	optionVar -stringValue "footControl"
		`textFieldButtonGrp -query -text footControlBtn`;

	optionVar -stringValue "kneeControl"
		`textFieldButtonGrp -query -text kneeControlBtn`;

	if ($doIt) 
	{
		performFootSetup 0;
		string $tmpCmd = "performFootSetup 0 ";
		addToRecentCommandQueue $tmpCmd "Setup Foot";
	}
}

proc footSetupOptions ()
{
	string $commandName = "footSetup";
	string $callback = ($commandName + "Callback");
	string $setup = ($commandName + "Setup");
	//	STEP 1:  Get the option box.
	//	============================
	//
	string $layout = getOptionBox();
	setParent $layout;
	//	STEP 2:  Pass the command name to the option box.
	//	=================================================
	//
	setOptionBoxCommandName($commandName);
	//	STEP 3:  Activate the default UI template.
	//	==========================================
	//
	setUITemplate -pushTemplate DefaultTemplate;
	//	STEP 4: Create option box contents.
	//	===================================
	//
	waitCursor -state 1;
	
	string $parent = `columnLayout -adjustableColumn 1`;

	/*
	frameLayout -label "Image" 
		-borderStyle "etchedIn"
		-collapsable 0;
	*/

	columnLayout;

	radioButtonGrp
		-numberOfRadioButtons 2
		-label "Heel Joint"
		-labelArray2 "Does not Exist" "Exists"
		-select 2
		footSetupHeel;

	radioButtonGrp
		-numberOfRadioButtons 3
		-label "Foot Style"
		-labelArray3 "IK Handles" "Parent Constraint" "Set Driven Key"
		-select 1
		footSetupStyle;

	textFieldButtonGrp -label "Foot Control" 
		//-text "Driver to copy to" 
		-buttonLabel "Set"
		-buttonCommand "getFootSetupControl foot"
		-editable true
		-annotation ("Select the foot control object or leave blank for the default control.")
		footControlBtn;

	textFieldButtonGrp -label "Knee Control" 
		//-text "Driver to copy to" 
		-buttonLabel "Set"
		-buttonCommand "getFootSetupControl knee"
		-editable true
		-annotation ("Select the knee control object or leave blank for the default control.")
		kneeControlBtn;
	
	setParent ..;

	setParent ..;

	//copyAttrib
	
	setParent -menu ..;

	waitCursor -state 0;
	//	Step 5: Deactivate the default UI template.
	//	===========================================
	//
	setUITemplate -popTemplate;

	//	Step 6: Customize the buttons.  
	//	==============================
	//
	string $applyBtn = getOptionBoxApplyBtn();
	button -edit 
		-label "Apply"
		-command ($callback + " " + $parent + " " + 1) 
		$applyBtn;
	
	string $applyAndCloseBtn = getOptionBoxApplyAndCloseBtn();
	button -edit 
		-label "Setup Foot"
		$applyAndCloseBtn;

	string $saveBtn = getOptionBoxSaveBtn();
	button -edit 
		-command ($callback + " " + $parent + " " + 0 + " " +"; hideOptionBox")
		$saveBtn;

	string $resetBtn = getOptionBoxResetBtn();
	button -edit 
		-command ($setup + " " + $parent + " " + 1)
		$resetBtn;

	//	Step 7: Set the option box title.
	//	=================================
	//
	setOptionBoxTitle("Setup Foot Options");
	//	Step 8: Customize the 'Help' menu item text.
	//	============================================
	//
	//setOptionBoxHelpTag("Poly Attribute Map");
	//	Step 9: Set the current values of the option box.
	//	=================================================
	//
	eval (($setup + " " + $parent + " " + 0));
	//	Step 10: Show the option box.
	//	=============================
	//
	showOptionBox();
}


proc string assembleCmd()
{
	setOptionVars (false);

	int $doHistory = `constructionHistory -q -toggle`;
	int $heelValue = `optionVar -query "footSetupHeel"`;
	int $footStyleValue = `optionVar -query "footSetupStyle"`;
	string $footControl = `optionVar -query "footControl"`;
	string $kneeControl = `optionVar -query "kneeControl"`;
	$heelValue = $heelValue -1;
	string $footStyle;
	if ($footStyleValue == 1)
		$footStyle = "ikHandles";
	else if ($footStyleValue == 2)
		$footStyle = "parentConstraint";
	else if ($footStyleValue == 3)
		$footStyle = "sdkFoot";
	else
		$footStyle = "ikHandles";

	string $cmd = "footSetup ";
	$cmd += $heelValue;
	$cmd += " \"";
	$cmd += $footStyle;
	$cmd += "\" \"";
	$cmd += $footControl;
	$cmd += "\" \"";
	$cmd += $kneeControl;
	$cmd += "\"";

	print $cmd;
	return $cmd;	
}


global proc string performFootSetup (int $option)
{
	string $cmd="";

	switch ($option) 
	{
		case 0:
			$cmd = `assembleCmd`;
			eval($cmd);
			break;
		case 1:
			footSetupOptions;
			break;
		case 2:
			$cmd = `assembleCmd`;
			break;
	}
	return $cmd;
}
