// Author:		Steven T. L. Roselle
//
// Last Update:        ( 04/01/14 )

global proc float bt_edgeLengthCalc()
{
 string $edges[] = `filterExpand -sm 32`;
 float $edgeLength = 0.0;

 string $vertices[];
 float $v1[3];
 float $v2[3];
 float $edgeFloat[3];
 vector $edgeVector;
 float $tmpEdgeLength;
 string $attached[];

 if ( `size($edges)` ){
  for( $thisEdge in $edges ){
    $attached = `polyListComponentConversion -ff -fe -fuv -fvf -tv $thisEdge`;
    
    $vertices = `filterExpand -sm 31 $attached`;

    $v1 = `pointPosition $vertices[0]`;
    $v2 = `pointPosition $vertices[1]`;
    
    for ( $i = 0; $i < 3; $i++ ){
	$edgeFloat[$i] = $v1[$i] - $v2[$i];
    }
    $edgeVector = $edgeFloat;
    
    $tmpEdgeLength = `mag $edgeVector`;
    $edgeLength += $tmpEdgeLength;
    print ("Edge length = " + $edgeLength + "\n");
  }
 }
 else {
  $edgeLength = 0.0;
 }

    return $edgeLength;
}

proc bt_createEdgeLengthHUD (){

	headsUpDisplay
		-section          0
		//-block            9
		-b `headsUpDisplay -nfb  0`
		-blockSize        "small"
		-label            "Edge Length:"
		-labelFontSize    "small"
		-dataFontSize	  "small"
		-command          "bt_edgeLengthCalc"
		-event            "SelectionChanged"
		-nodeChanges      "attributeChange"
		-dp		4
	HUDEdgeLength;
}

global proc bt_toggleEdgeLengthHUD (){

    if (`headsUpDisplay -ex HUDEdgeLength`)
	headsUpDisplay -rem HUDEdgeLength;
    else 
        bt_createEdgeLengthHUD;
     	
}
